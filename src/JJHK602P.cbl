000010 IDENTIFICATION                   DIVISION.
000020 PROGRAM-ID.                      JJHK602P.
000030*****************************************************************
000040*  業務名        ：WebRings 国民健康保険
000050*
000060*  顧客名        ：越谷市
000070*
000080*  プロセス名    ：納税通知書（現年度一般用）
000090*
000100*  処理名        ：納税通知書のＰＤＦ化
000110*
000120*  処理概要      ：納税通知書（現年度一般用）プリントファイルを
000130*                  読み込み、納税通知書を編集・出力する。
000140*
000150*  実行時コード系：UCS2
000160*
000170*  文字コード    ：ﾃﾞｰﾀ半角項目    ：SJIS→Unicode(UTF8)変換
000180*                  日本語ﾃﾞｰﾀ項目  ：Unicode
000190*
000200*  作表方式      ：帳票漢字項目    ：FUJ明朝体ﾌｫﾝﾄ埋め込み方式
000210*                  帳票定義体形式  ：ページ方式（ﾊﾟｰﾃｨｼｮﾝあり）
000220*
000230*  帳票定義体    ：JJHK602.pmd
000240*
000250*  作成者        ：INES / 菊池善彦
000260*
000270*  作成日        ：2013.07.09
000280*
000290*  修正履歴      ：
000300*    修正年月日  -----------------  修正内容  -------------------
000310*    2022/02/18  金石東  案件ID:76360  @履歴ID:220215KI
000320*                リコンパイルのみ実施
000330*    2023/10/06  棚橋    案件ID:77024  @履歴ID:231006MT
000340*                リコンパイルのみ実施
000350*    2025/12/22　アイネス　トウン　案件ID：77480　@履歴ID:251222TL
000360*                JJ令和8年度法改正納税通知書改修対応
000370*                JJHK602.PMD 変更に伴うリコンパイル
000380*
000390*****************************************************************
000400*                    環　　境　　部
000410*****************************************************************
000420 ENVIRONMENT                      DIVISION.
000430*****************************************************************
000440*================================================================
000450 CONFIGURATION                    SECTION.
000460*================================================================
000470 SOURCE-COMPUTER.                 F-PC
000480*DBG*           WITH  DEBUGGING  MODE
000490     .
000500 OBJECT-COMPUTER.                 F-PC.
000510*
000520*================================================================
000530 INPUT-OUTPUT                     SECTION.
000540*================================================================
000550 FILE-CONTROL.
000560      SELECT   LST-F
000570          ASSIGN  TO  PRTFILE
000580          FORMAT  IS  LST-FORMAT
000590          GROUP   IS  LST-GROUP
000600          FILE STATUS  IS  LST-STATUS  詳細コード.
000610*
000620*****************************************************************
000630 DATA                             DIVISION.
000640*****************************************************************
000650*================================================================
000660 FILE                             SECTION.
000670*================================================================
000680*（ＯＵＴ）
000690 FD  LST-F.
000700      COPY    JJHK602             OF  XMDLIB.
000710*================================================================
000720 WORKING-STORAGE                  SECTION.
000730*================================================================
000740 01  ABEND                        PIC  X(006)  VALUE 'ABENDU'.
000750*----------------------------------------------------------------
000760*    入出力定義領域
000770*----------------------------------------------------------------
000780*（ＩＮ）
000790 01  PRINT-DATA-LEN               PIC S9(009).
000800 01  PARM-KAMA021A-PRINTIN.
000810   03  入力ＤＤ名１３             PIC  X(008)  VALUE 'PRINTIN'.
000820   03  入力処理方法１３           PIC  X(008)  VALUE SPACE.
000830   03  入力ＲＬ１３               PIC S9(009)  COMP-5.
000840   03  入力ＢＬ１３               PIC S9(009)  COMP-5.
000850   03  入力件数１３               PIC S9(009)  COMP-5.
000860   03  ＥＯＦ情報１３             PIC  X(004)  VALUE SPACE.
000870*
000880 01  U13-REC.
000890   03  制御エリア.
000900     05  改行指示                 PIC  X(006).
000910     05  行数Ｒ.
000920       07   行数                  PIC  9(004).
000930   03  行パターン                 PIC  X(030).
000940   03  プリントデータ.
000950     05  プリントデータ－内容     PIC  X(001)
000960                                  OCCURS  1  TO   32760 TIMES
000970                                  DEPENDING  ON   PRINT-DATA-LEN.
000980*
000990*----------------------------------------------------------------
001000*    固定値領域
001010*----------------------------------------------------------------
001020 01  CPID                         PIC  X(008)  VALUE 'JJHK602P'.
001030 01  Ｃ帳票定義体名通知域         PIC  X(008)  VALUE 'JJHK602'.
001040 01  Ｃ項目群通知域１             PIC  X(008)  VALUE 'GROUP1'.
001050 01  Ｃ項目群通知域２             PIC  X(008)  VALUE 'GROUP2'.
001060 01  Ｃ項目群通知域３             PIC  X(008)  VALUE 'GROUP3'.
001070 01  Ｃ項目群通知域４             PIC  X(008)  VALUE 'GROUP4'.
001080 01  CNS-1                        PIC  9(001)  VALUE 1.
001090*
001100*----------------------------------------------------------------
001110*    帳票制御領域
001120*----------------------------------------------------------------
001130 01  LST-FORMAT                   PIC  X(008).
001140 01  LST-GROUP                    PIC  X(008).
001150 01  LST-STATUS                   PIC  X(002).
001160 01  詳細コード                   PIC  X(004).
001170 01  帳票ページ数                 PIC S9(009)  COMP-5 VALUE ZERO.
001180 01  出力対象ページ数             PIC S9(009)  COMP-5 VALUE ZERO.
001190*
001200* ＳＪＩＳコード半角カナ文字列固定値
001210   COPY    SJISKANA.
001220*
001230 01  ＷＫその他領域.
001240   03  ＷＫ処理日                 PIC  X(008).
001250   03  ＷＫ処理時                 PIC  9(008).
001260*
001270*----------------------------------------------------------------
001280 LINKAGE                          SECTION.
001290*----------------------------------------------------------------
001300*
001310 01  PARM-AREA.
001320     COPY  PCBPARMP.
001330*
001340/****************************************************************
001350 PROCEDURE                        DIVISION
001360                                  USING PARM-AREA.
001370*****************************************************************
001380* ファイルアクセスエラー時の誤り処理手続き
001390     COPY  PCBFILERR
001400           REPLACING  ==@@@@@@@@@@==  BY  ==LST-F==
001410                      ==((((((((((==  BY  ==LST-STATUS==
001420                      ==))))))))))==  BY  ==詳細コード==.
001430*****************************************************************
001440*================================================================
001450 メイン処理                       SECTION.
001460*================================================================
001470 メイン処理－開始.
001480*
001490     PERFORM  前処理.
001500*
001510     PERFORM  主処理  UNTIL  ＥＯＦ情報１３  =  Ｃオワリ.
001520*
001530     PERFORM  後処理.
001540*
001550 メイン処理－終了.
001560     STOP RUN.
001570*================================================================
001580 前処理                           SECTION.
001590*================================================================
001600 前処理－開始.
001610*
001620     DISPLAY '*** ' CPID ' START VER.'
001630              FUNCTION  WHEN-COMPILED(1:8) ' ***'  UPON  SYSOUT.
001640*
001650* 帳票出力の共通実行パラメータのチェック(PCBPRPCK.CPYと対応)
001660     COPY  PCBPRPCK.
001670*
001680* 使用変数、ワーク領域の初期化
001690     INITIALIZE                       ＷＫその他領域.
001700*
001710     PERFORM  日付時刻表示.
001720*
001730     PERFORM  ファイルオープン.
001740*
001750     PERFORM  帳票ファイル読込処理.
001760*
001770 前処理－終了.
001780     EXIT.
001790*================================================================
001800 ファイルオープン                 SECTION.
001810*================================================================
001820 ファイルオープン－開始.
001830*
001840     COMPUTE  PRINT-DATA-LEN      =   FUNCTION LENG(JJHK602).
001850     MOVE  'OPEN'                 TO  入力処理方法１３.
001860     COMPUTE  入力ＲＬ１３        =   FUNCTION  LENG(U13-REC).
001870*
001880     CALL  'KAMA021A'          USING  PARM-KAMA021A-PRINTIN.
001890*
001900     OPEN  OUTPUT  LST-F.
001910*
001920 ファイルオープン－終了.
001930     EXIT.
001940*================================================================
001950 帳票ファイル読込処理             SECTION.
001960*================================================================
001970 帳票ファイル読込処理－開始.
001980*
001990     MOVE  'READ'                 TO  入力処理方法１３.
002000     CALL  'KAMA021A'          USING  PARM-KAMA021A-PRINTIN
002010                                      U13-REC.
002020     IF      ＥＯＦ情報１３  =  Ｃオワリ
002030        GO TO    帳票ファイル読込処理－終了
002040     END-IF.
002050*
002060     IF  行パターン(1:6)  =  Ｃ項目群通知域１(1:6)
002070         ADD   CNS-1               TO  出力対象ページ数
002080     END-IF.
002090*
002100*    ******************************************************
002110*    * 帳票出力範囲チェックの共通処理(PCBPARMP.CPYと対応) *
002120*    ******************************************************
002130     IF  PARM-PRINT-S  > ZERO
002140       IF  出力対象ページ数  >=  PARM-PRINT-S
002150         IF   PARM-PRINT-E  <  999999999
002160           IF  出力対象ページ数  <=  PARM-PRINT-E
002170             CONTINUE
002180           ELSE
002190             GO TO    帳票ファイル読込処理－開始
002200           END-IF
002210         ELSE
002220           CONTINUE
002230         END-IF
002240       ELSE
002250         GO TO    帳票ファイル読込処理－開始
002260       END-IF
002270     ELSE
002280       CONTINUE
002290     END-IF.
002300*
002310*
002320 帳票ファイル読込処理－終了.
002330     EXIT.
002340*================================================================
002350 主処理                           SECTION.
002360*================================================================
002370 主処理－開始.
002380*
002390     PERFORM  帳票出力処理.
002400*
002410     PERFORM  帳票ファイル読込処理.
002420*
002430 主処理－終了.
002440     EXIT.
002450*================================================================
002460 帳票出力処理                     SECTION.
002470*================================================================
002480 帳票出力処理－開始.
002490*
002500     MOVE  プリントデータ         TO  JJHK602.
002510     MOVE  Ｃ帳票定義体名通知域   TO  LST-FORMAT.
002520     EVALUATE  行パターン(1:6)
002530       WHEN  Ｃ項目群通知域１(1:6)
002540         MOVE  Ｃ項目群通知域１   TO  LST-GROUP
002550*        帳票ページ数のカウンタはGROUP1の出力時にアップ
002560         ADD  CNS-1               TO  帳票ページ数
002570       WHEN  Ｃ項目群通知域２(1:6)
002580         MOVE  Ｃ項目群通知域２   TO  LST-GROUP
002590       WHEN  Ｃ項目群通知域３(1:6)
002600         MOVE  Ｃ項目群通知域３   TO  LST-GROUP
002610       WHEN  Ｃ項目群通知域４(1:6)
002620         MOVE  Ｃ項目群通知域４   TO  LST-GROUP
002630     END-EVALUATE.
002640*
002650     WRITE  JJHK602   AFTER  PAGE.
002660*
002670 帳票出力処理－終了.
002680     EXIT.
002690*================================================================
002700 後処理                           SECTION.
002710*================================================================
002720 後処理－開始.
002730*
002740     IF  入力処理方法１３  NOT = SPACE
002750         MOVE  'CLOSE'            TO  入力処理方法１３
002760         CALL  'KAMA021A'      USING  PARM-KAMA021A-PRINTIN
002770     ELSE
002780         CONTINUE
002790     END-IF.
002800*
002810     CLOSE  LST-F.
002820*
002830     DISPLAY '(INPUT) '
002840             'PRINTIN:入力ファイル = '
002850             入力件数１３  ' 件'                 UPON  SYSOUT.
002860*
002870     DISPLAY '(OUTPUT)'
002880             'PRTFILE:出力帳票     = '
002890             帳票ページ数  ' 頁'                 UPON  SYSOUT.
002900*
002910     PERFORM  日付時刻表示.
002920*
002930     DISPLAY '*** ' CPID ' END ***'              UPON  SYSOUT.
002940*
002950     DISPLAY ' '                                 UPON  SYSOUT.
002960*
002970 後処理－終了.
002980     EXIT.
002990*================================================================
003000 日付時刻表示                     SECTION.
003010*================================================================
003020 日付時刻表示－開始.
003030*
003040     PERFORM 日付時刻取得.
003050*
003060     DISPLAY '(' CPID ') '
003070          ＷＫ処理日(1:4) '/' ＷＫ処理日(5:2) '/' ＷＫ処理日(7:2)
003080      ' ' ＷＫ処理時(1:2) ':' ＷＫ処理時(3:2) ':' ＷＫ処理時(5:2)
003090                                                 UPON  SYSOUT.
003100*
003110 日付時刻表示－終了.
003120     EXIT.
003130*================================================================
003140 日付時刻取得                     SECTION.
003150*================================================================
003160 日付時刻取得－開始.
003170*
003180     MOVE  FUNCTION  CURRENT-DATE
003190                                  TO  ＷＫ処理日.
003200     ACCEPT  ＷＫ処理時         FROM  TIME.
003210*
003220 日付時刻取得－終了.
003230     EXIT.
003240*****************************************************************
003250*              << JJHK602P PROGRAM END >>                       *
003260*****************************************************************
003270 END PROGRAM JJHK602P.
